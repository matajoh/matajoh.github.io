<html lang="en">

<head>
    <title>Canny Edge Detection</title>
    <meta charset="utf-8" />
    <link rel="stylesheet" type="text/css" href="canny.css" />
    <link rel="stylesheet" href="scripts/styles/atom-one-dark.min.css">
    <viewport content="width=device-width, initial-scale=1.0" />
</head>

<body>
    <div class="container">
        <h1>Canny Edge Detection</h1>
        <div class="controls">
            <label for="threshold">Threshold:</label>
            <input type="range" min="1" max="30" value="10" class="slider" id="threshold">
        </div>
        <div id="https" class="warning">
            <p>Notice: the camera will not function if unless you connect via an HTTPS link.</p>
        </div>
        <canvas id="canvas"></canvas>
        <div class="controls">
            <label for="source">Camera</label>
            <select id="source" onchange="switchCamera()" >
                <option value="environment" selected="selected">Back</option>
                <option value="user">Front</option>
            </select>
            <label for="flipped">Flipped</label>
            <input type="checkbox" id="flipped">
        </div>
        <div class="controls">
            <label for="showCode">Show Code:</label>
            <input type="checkbox" id="showCode"
                onchange="document.getElementById('code').style.visibility = this.checked ? 'visible' : 'collapse'">
        </div>
        <div id="code" style="visibility: collapse">
            <h3>Fragment Shader</h3>
            <pre>
            <code>
precision mediump float;

vec4 blur(sampler2D image, vec2 uv, vec2 resolution, vec2 direction) {
    vec4 color = vec4(0.0);
    vec2 off1 = vec2(1.3333333333333333) * direction;
    color += texture2D(image, uv) * 0.29411764705882354;
    color += texture2D(image, uv + (off1 / resolution)) * 0.35294117647058826;
    color += texture2D(image, uv - (off1 / resolution)) * 0.35294117647058826;
    return color; 
}

float grayscale(vec4 color) {
  return 0.2989 * color.r + 0.5870 * color.g + 0.1140 * color.b;
}

vec2 gradient_direction(vec2 g)
{
  float pi = 3.1415926535897932384626433832795;
  float pi4 = pi / 4.0;
  float theta = atan(g.y, g.x);
  theta -= pi / 8.0;
  theta = theta &lt; 0.0 ? theta + 2.0 * pi : theta;
  theta = pi4 * floor(theta / pi4);
  return vec2(cos(theta), sin(theta));
}

vec2 gradient(sampler2D textureSampler, vec2 textureCoord, vec2 resolution) {
    vec2 dx = vec2(1.0, 0.0) / resolution.x;
    vec2 dy = vec2(0.0, 1.0) / resolution.y;

    float left = grayscale(blur(textureSampler, textureCoord - dx, resolution, vec2(1.0, 0.0)));
    float right = grayscale(blur(textureSampler, textureCoord + dx, resolution, vec2(1.0, 0.0)));
    float top = grayscale(blur(textureSampler, textureCoord - dy, resolution, vec2(0.0, 1.0)));
    float bottom = grayscale(blur(textureSampler, textureCoord + dy, resolution, vec2(0.0, 1.0)));

    return vec2(right - left, bottom - top);
}

vec4 cannyEdgeDetection(
    sampler2D textureSampler,
    vec2 textureCoord,
    vec2 resolution,
    float weakThreshold,
    float strongThreshold
) {
    vec2 g = gradient(textureSampler, textureCoord, resolution);
    float grad = length(g);

    if(grad &lt; weakThreshold){
        return vec4(vec3(0), 1.0);
    }

    vec2 gradDir = gradient_direction(g) / resolution;
    vec2 offDir = vec2(-gradDir.y, gradDir.x);

    float grad0 = length(gradient(textureSampler, textureCoord + offDir, resolution));
    float grad1 = length(gradient(textureSampler, textureCoord - offDir, resolution));

    if(grad &lt; grad0 || grad &lt; grad1){
        return vec4(vec3(0), 1.0);
    }

    if(grad >= strongThreshold){
        return vec4(vec3(1), 1.0);
    }

    grad0 = length(gradient(textureSampler, textureCoord + gradDir, resolution));
    grad1 = length(gradient(textureSampler, textureCoord - gradDir, resolution));

    if(grad0 >= strongThreshold || grad1 >= strongThreshold){
        return vec4(vec3(1), 1.0);
    }

    return vec4(vec3(0), 1.0);
}

uniform sampler2D uSampler;
uniform vec2 uResolution;
uniform float uWeakThreshold;
uniform float uStrongThreshold;
uniform bool uFlipped;

varying vec2 vTexCoord;

void main() {
  vec2 uv = vTexCoord;

  uv.y = 1.0 - uv.y;
  if(uFlipped){
    uv.x = 1.0 - uv.x;
  }
 
  gl_FragColor = cannyEdgeDetection(uSampler, uv, uResolution, uWeakThreshold, uStrongThreshold);
}
            </code>
        </pre>
            <h3>Vertex Shader</h3>
            <pre>
            <code>
precision mediump float;

attribute vec3 aPosition;
attribute vec2 aTexCoord;

varying vec2 vTexCoord;

void main() {
    vTexCoord = aTexCoord;

    vec4 positionVec4 = vec4(aPosition, 1.0);
    positionVec4.xy = positionVec4.xy * 2.0 - 1.0;

    gl_Position = positionVec4;
}
            </pre>
            </code>
        </div>
    </div>

    <script src="scripts/p5.min.js"></script>
    <script src="scripts/highlight.min.js"></script>

    <script>
        let theShader;
        let cam;
        let width = 640;
        let height = 480;

        if(location.protocol !== 'https:') {
            document.getElementById('https').style.display = 'block';
        }

        function preload() {
            theShader = loadShader('canny.vert', 'canny.frag');
        }

        function setup() {
            const isPortrait = window.matchMedia('(orientation: portrait)').matches;

            if (isPortrait) {
                width = 480;
                height = 640;
            }

            canvas = document.getElementById('canvas');
            createCanvas(width, height, WEBGL, canvas);
            noStroke();

            cam = createCapture({
                "audio": false,
                "video": {
                    "facingMode": document.getElementById('source').value
                }
            });
            cam.size(width, height);

            cam.hide();
        }

        function switchCamera() {
            cam.remove();
            cam = createCapture({
                "audio": false,
                "video": {
                    "facingMode": document.getElementById('source').value
                }
            });
            cam.size(width, height);
            cam.hide();
        }

        function draw() {
            shader(theShader);

            let strongThreshold = document.getElementById('threshold').value / 100;
            let weakThreshold = strongThreshold * 0.7;
            let flipped = document.getElementById('source').value === 'user';
            if(document.getElementById('flipped').checked){
                flipped = !flipped;
            }

            theShader.setUniform('uSampler', cam);
            theShader.setUniform('uWeakThreshold', weakThreshold);
            theShader.setUniform('uStrongThreshold', strongThreshold);
            theShader.setUniform('uResolution', [width, height]);
            theShader.setUniform('uFlipped', flipped);

            // rect gives us some geometry on the screen
            rect(0, 0, width, height);

        }

        hljs.highlightAll();
    </script>
</body>

</html>